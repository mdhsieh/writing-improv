apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
// Enable safe args plugin
// to send data between Fragments in compile-safe way
apply plugin: "androidx.navigation.safeargs.kotlin"
// Enable Google services plugin to use Firebase
apply plugin: 'com.google.gms.google-services'
android {
    compileSdkVersion 30
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.michaelhsieh.writingimprov"
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        // use VectorDrawableCompat to load vector drawables
        vectorDrawables.useSupportLibrary = true

        // Enabling Multidex support.
        // Adding Google services to use Firestore created too many methods
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    // Enable Java 8 options so Navigation Component works properly
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

    // load images
    implementation 'com.squareup.picasso:picasso:2.71828'

    // update Android's security Provider to fix Picasso image load error
    implementation 'com.google.android.gms:play-services-auth:17.0.0'

    // log
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // Toast
    implementation 'com.github.GrenderG:Toasty:1.5.0'

    // Navigation Component
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    // Required to use GsonConverterFactory.create()
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    // force OkHttp 3.12 to fix error on older devices, ex. API 17 tablet
    // java.lang.ExceptionInInitializerError
    //        at okhttp3.OkHttpClient.newSslSocketFactory(OkHttpClient.java:263)
    // and another error:
    // ssl handshake aborted
    implementation("com.squareup.okhttp3:okhttp:3.12.8") {
        force = true
    }

    // Import the Firebase BoM
    implementation platform('com.google.firebase:firebase-bom:28.0.1')
    // When using the BoM, don't specify versions in Firebase dependencies
    // Declare the dependency for the Cloud Firestore library
    implementation 'com.google.firebase:firebase-firestore-ktx'

    // Enabling Multidex support on older devices, example API 17 tablet
    implementation 'androidx.multidex:multidex:2.0.1'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'

    // Espresso
    def androidx_test_espresso = "3.3.0"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidx_test_espresso"

    // androidx.test
    def androidx_test = "1.1.0"
    androidTestImplementation "androidx.test:runner:$androidx_test"
    androidTestImplementation "androidx.test:core:$androidx_test"
    androidTestImplementation "androidx.test.ext:junit-ktx:$androidx_test"

    // idling resource
    // use implementation since class needs to be in application code
    implementation "androidx.test.espresso:espresso-idling-resource:$androidx_test_espresso"

    // Try use this to test Fragments
    def fragment_version = "1.3.4"
    debugImplementation "androidx.fragment:fragment-testing:$fragment_version"
}